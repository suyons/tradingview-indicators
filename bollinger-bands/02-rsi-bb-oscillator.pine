//@version=5
indicator('RSI + BB Oscillator', 'RSI + BB Oscillator', false)

// Input
tf_1 = input.timeframe('', '', inline = '1', group = 'TIMEFRAME')
tf_2 = input.timeframe('60', '', inline = '1', group = 'TIMEFRAME')

line_val_1 = input.int(50, '', inline = '1', group = 'LINE')
line_val_2 = input.int(30, '', inline = '1', group = 'LINE')
line_val_3 = input.int(10, '', inline = '1', group = 'LINE')
line_val_4 = input.int(-10, '', inline = '2', group = 'LINE')
line_val_5 = input.int(-30, '', inline = '2', group = 'LINE')
line_val_6 = input.int(-50, '', inline = '2', group = 'LINE')

rsi_len = input.int(14, 'Length', inline = '1', group = 'RSI: CONFIG')
rsi_src = input.source(close, 'Source', inline = '1', group = 'RSI: CONFIG')

bb_ma_type = input.string(title="Type", defval="HMA", inline="1", options = ["SMA", "EMA", "WMA", "HMA", "RMA", "VWMA", "ALMA"], group="BAND: CONFIG")
bb_src = input(close, title="Source", inline="1", group="BAND: CONFIG")
bb_ma_len = input.int(120, "Basis Len", minval=1, inline="2", group="BAND: CONFIG")
bb_dev_len = input.int(120, "Stdev Len", minval=1, inline="2", group="BAND: CONFIG")
bb_dev_mult = input.float(2.0, "Stdev Mult", inline="3", group="BAND: CONFIG")

sig_lookback = input.int(10, 'Lookback', inline = '1', group = 'SIGNAL: CONFIG')

// Fetch
rsi_1 = request.security(syminfo.tickerid, tf_1, ta.rsi(rsi_src, rsi_len)) - 50
rsi_2 = request.security(syminfo.tickerid, tf_2, ta.rsi(rsi_src, rsi_len)) - 50

band(src, type, ma_len, dev_len) =>
    basis = switch type
        "SMA" => ta.sma(src, ma_len)
        "EMA" => ta.ema(src, ma_len)
        "WMA" => ta.wma(src, ma_len)
        "HMA" => ta.hma(src, ma_len)
        "RMA" => ta.rma(src, ma_len)
        "VWMA" => ta.vwma(src, ma_len)
        "ALMA" => ta.alma(src, ma_len, 1, 0)
    stdev = ta.stdev(src, dev_len)
    upper = basis + stdev * bb_dev_mult
    lower = basis - stdev * bb_dev_mult
    perb = 100 * (src - basis) / (stdev * bb_dev_mult * 2)
    [basis, upper, lower, perb]

[basis_1, upper_1, lower_1, perb_1] = request.security(syminfo.tickerid, tf_1, band(bb_src, bb_ma_type, bb_ma_len, bb_dev_len))
[basis_2, upper_2, lower_2, perb_2] = request.security(syminfo.tickerid, tf_2, band(bb_src, bb_ma_type, bb_ma_len, bb_dev_len))

// Condition
trend_up = rsi_2 > line_val_3
trend_side = line_val_3 > rsi_2 and rsi_2 > line_val_4
trend_down = line_val_4 > rsi_2

buy = (trend_down or trend_side) and perb_1 < line_val_6
sell = (trend_up or trend_side) and perb_1 > line_val_1

lookback(side, len) =>
    bool result = false
    for i = 1 to len
        if side[i]
            result := true
    result

buy_condition = buy and not lookback(buy, sig_lookback)
sell_condition = sell and not lookback(sell, sig_lookback)

// Plot
plot(perb_1, color=color.new(#00e676, 0), linewidth=1, title='%B 1')
plot(perb_2, color=color.new(#ff9800, 0), linewidth=2, title='%B 2')
plot(rsi_1, color=color.new(#00bcd4, 0), linewidth=1, title='RSI 1')
plot(rsi_2, color=color.new(#e040fb, 0), linewidth=2, title='RSI 2')

hline(line_val_1, title = "Line 1", color = color.new(color.white, 0), linestyle = hline.style_dotted, linewidth = 1)
hline(line_val_2, title = "Line 2", color = color.new(color.white, 25), linestyle = hline.style_dotted, linewidth = 1)
hline(line_val_3, title = "Line 3", color = color.new(color.white, 50), linestyle = hline.style_dotted, linewidth = 1)
hline(line_val_4, title = "Line 4", color = color.new(color.white, 50), linestyle = hline.style_dotted, linewidth = 1)
hline(line_val_5, title = "Line 5", color = color.new(color.white, 25), linestyle = hline.style_dotted, linewidth = 1)
hline(line_val_6, title = "Line 6", color = color.new(color.white, 0), linestyle = hline.style_dotted, linewidth = 1)
hline(0, title = "Middle", color = color.white, linestyle = hline.style_solid, linewidth = 1)

if buy_condition
    line.new(x1 = bar_index, y1 = line_val_1, x2 = bar_index, y2 = line_val_6, color = color.new(color.green, 50), style = line.style_solid, width = 4)
    alert('{"symbol":"'+syminfo.ticker+'","side":"Buy","price":'+str.tostring(close)+'}', alert.freq_once_per_bar_close)

if sell_condition
    line.new(x1 = bar_index, y1 = line_val_1, x2 = bar_index, y2 = line_val_6, color = color.new(color.red, 50), style = line.style_solid, width = 4)
    alert('{"symbol":"'+syminfo.ticker+'","side":"Sell","price":'+str.tostring(close)+'}', alert.freq_once_per_bar_close)
